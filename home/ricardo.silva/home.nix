{
  pkgs,
  inputs,
  config,
  ...
}:

let
  user = "ricardo.silva";
  gitName = "Ricardo Silva";
  gitEmail = "ricardo.silva@freiheit.com";
in
{
  imports = [
    inputs."home-manager".darwinModules.home-manager
  ];

  home-manager.useGlobalPkgs = true;
  home-manager.users.${user} = {
    home.stateVersion = "25.05";
    home.enableNixpkgsReleaseCheck = false;
    home.packages = with pkgs; [
      bat
      dockutil
      eza
      fzf
      ripgrep
      skhd
      starship
      yabai
      zoxide
    ];

    xdg.enable = true;

    xdg.configFile."yabai/yabairc" = {
      source = "${inputs.dotfiles}/yabai/yabairc";
      executable = true;
    };
    xdg.configFile."skhd/skhdrc" = {
      source = "${inputs.dotfiles}/skhd/skhdrc";
      executable = true;
    };

    programs.git = {
      enable = true;
      userName = gitName;
      userEmail = gitEmail;

      lfs.enable = true;
      delta.enable = true;
      delta.options = {
        syntax-theme = "GitHub";
        line-numbers = true;
        navigate = true;
      };

      extraConfig = {
        init.defaultBranch = "main";
        core = {
          editor = "code";
          autocrlf = "input";
        };
        pull.rebase = true;
        rebase.autoStash = true;
      };
    };

    programs.zsh = {
      enable = true;
      autosuggestion.enable = true;
      enableCompletion = true;
      syntaxHighlighting.enable = true;

      history = {
        size = 10000;
        save = 10000;
        ignoreDups = true;
        extended = true;
      };

      shellAliases = {
        cat = "bat -p";
        grep = "rg";
        ll = "eza -lah --icons";
        ls = "eza --group-directories-first";
        nix-apply = "sudo nix run nix-darwin -- switch --flake ~/.nix-config/.#phantom-one";
        nix-rebuild = "sudo darwin-rebuild switch --flake ~/.nix-config/.#phantom-one";
        tree = "eza --tree";
        colima-start-emulation = "colima start --arch x86_64 --vz-rosetta";
      };

      initContent = ''
        # set less annoying keybindings for fzf
        export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
        export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
        export FZF_DEFAULT_OPTS='--height 40% --border'

        # zoxide   
        eval "$(zoxide init zsh)"

        # starship prompt
        eval "$(starship init zsh)"


        # Generated by Hermit; START; DO NOT EDIT.
        HERMIT_ROOT_BIN="''${HERMIT_ROOT_BIN:-"$HOME/bin/hermit"}"
        eval "$(test -x $HERMIT_ROOT_BIN && $HERMIT_ROOT_BIN shell-hooks --print --zsh)"
        # Generated by Hermit; END; DO NOT EDIT.

        # Setup colima docker sock
        export DOCKER_HOST="unix://$HOME/.config/colima/default/docker.sock"
        export TESTCONTAINERS_DOCKER_SOCKET_OVERRIDE="unix://$HOME/.config/colima/default/docker.sock"
      '';

      oh-my-zsh = {
        enable = true;
        plugins = [
          "colored-man-pages"
          "command-not-found"
          "git"
          "history"
          "fzf"
        ];
        theme = ""; # controlled by starship
      };
    };
  };

  services.yabai.enable = true;
  services.skhd.enable = true;

  local.dock = {
    enable = true;
    username = user;
    entries = [
      { path = "/Applications/Launchpad.app"; }
      { path = "/Applications/1Password.app"; }
      { path = "/Applications/Google Chrome.app"; }
      { path = "${pkgs.firefox}/Applications/Firefox.app"; }
      { path = "${config.users.users.${user}.home}/Applications/Chrome Apps.localized/Google Chat.app"; }
      { path = "/Applications/Microsoft Teams.app"; }
      { path = "/Applications/WhatsApp.app"; }
      { path = "/Applications/ChatGPT.app"; }
      { path = "/Applications/Xcode.app/Contents/Developer/Applications/Simulator.app"; }
      { path = "${pkgs.vscode}/Applications/Visual Studio Code.app"; }
      { path = "/Applications/Utilities/Terminal.app"; }
      { path = "/Applications/Postman.app"; }
      { path = "${pkgs.obsidian}/Applications/Obsidian.app"; }
      {
        path = "${config.users.users.${user}.home}/Google Drive/My Drive";
        section = "others";
        options = "--sort name --view grid --display stack";
      }
      {
        path = "${config.users.users.${user}.home}/Downloads";
        section = "others";
        options = "--sort name --view grid --display stack";
      }
    ];
  };
}
